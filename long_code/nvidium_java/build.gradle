plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
}

def gitCommitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
def buildtime = System.currentTimeSeconds()

version = project.mod_version
group = project.maven_group

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

apply plugin: "fabric-loom"

loom {
    mixin.defaultRefmapName = "nvidium.refmap.json"
    runs {
        it.configureEach {
            vmArgs("-Xmx8G", "-XX:+UseZGC")
        }
    }
}


processResources {
    inputs.properties("version": project.version, "commit": gitCommitHash, "buildtime":buildtime)

    archivesBaseName = "nvidium"
    filesMatching("fabric.mod.json") {
        expand "commit": gitCommitHash, "version": project.version, "buildtime": buildtime
    }
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API
    modImplementation(fabricApi.module("fabric-api-base", project.fabric_version))
    modImplementation(fabricApi.module("fabric-block-view-api-v2", project.fabric_version))
    modImplementation(fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version))

    modImplementation("net.fabricmc.fabric-api:fabric-rendering-data-attachment-v1:0.3.40+73761d2e3b")
    modImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_version))

    modImplementation "maven.modrinth:sodium:mc1.21-0.5.11"

    //modImplementation "maven.modrinth:c2me-fabric:0.2.0+alpha.10.49+1.19.4"
    //modImplementation "maven.modrinth:chunks-fade-in:v1.0.3-1.19.4"
    //modImplementation "maven.modrinth:immersiveportals:v2.7.3-mc1.19.4"
    modCompileOnly "maven.modrinth:iris:1.7.0+1.20.6"
}

